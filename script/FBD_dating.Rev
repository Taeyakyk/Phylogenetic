speciation_rate ~ dnExponential(10)
extinction_rate ~ dnExponential(10)
fossil_samp_rate ~ dnExponential(10)

# the moves
moves.append( mvScale(speciation_rate, lambda = 0.5, tune = true, weight = 3.0) )
moves.append( mvScale(extinction_rate, lambda = 0.5, tune = true, weight = 3.0) )
moves.append( mvScale(fossil_samp_rate, lambda = 0.5, tune = true, weight = 3.0) )

# diversification and tunrover rates
diversification := speciation_rate - extinction_rate
turnover := extinction_rate/speciation_rate

# the probability of extant species sampling (œÅ)
rho <- 1.0

# prior and move on the origin
origin_time ~ dnUnif(20.0, 30.0)
moves.append( mvSlide(origin_time, weight=1.0) )

# birth death tree prior
tree ~ dnFBDP(lambda = speciation_rate, mu = extinction_rate, psi = fossil_samp_rate, 
rho = rho, origin = origin_time, taxa = taxa)

# moves to search tree space
moves.append( mvFNPR(tree, weight = 15.0) )
moves.append( mvCollapseExpandFossilBranch(tree, origin_time, weight = 6.0) )
moves.append( mvNodeTimeSlideUniform(tree, weight = 40.0) )
moves.append( mvRootTimeSlideUniform(tree, origin_time, weight = 5.0) )

clade_extant = clade("Pygoscelis_adeliae","Pygoscelis_antarctica","Pygoscelis_papua", 
                    "Megadytes_antipodes","Eudyptes_chrysocome","Eudyptes_chrysolophus",
                    "Eudyptes_pachyrrhynchus","Eudyptes_minor","Aptenodytes_forsteri", 
                    "Aptenodytes_patagonicus", "Madrynornis_mirandus",
                    "Paraptenodytes_antarcticus", "Spheniscus_megaramphus")  

age_extant := tmrca(tree, clade_extant)
# how many sampled ancestors are in our posterior trees or another deterministic variable 
num_samp_anc := tree.numSampledAncestors()

fossils = tree.getFossils()

for(i in 1:fossils.size())
{
    t[i] := tmrca(tree, clade(fossils[i]))

    a_i = fossils[i].getMinAge()
    b_i = fossils[i].getMaxAge()

    F[i] ~ dnUniform(t[i] - b_i, t[i] - a_i)
    F[i].clamp( 0 )
}

# moves on fossil ages
moves.append( mvFossilTimeSlideUniform(tree, origin_time, weight = 5.0) )
